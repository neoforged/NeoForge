plugins {
    id 'java'
    id 'maven-publish'
    id 'net.neoforged.gradle.platform'
}

apply plugin: 'com.diffplug.spotless'
apply plugin: 'net.neoforged.licenser'

version = project(':neoforge').version
group = 'net.neoforged.neoforge'

repositories {
    maven {
        name 'Mojang'
        url 'https://libraries.minecraft.net'
    }
    maven {
        name 'NeoForged'
        url 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    compileOnly project(':neoforge')
    compileOnly 'org.junit.jupiter:junit-jupiter-api:5.10.1'
}

jar {
    manifest.attributes([
            'Automatic-Module-Name': 'neoforgejunitutils',
            'FMLModType'           : 'GAMELIBRARY'
    ])
}

license {
    header = rootProject.file('codeformat/HEADER.txt')
    skipExistingHeaders = true
    include '**/*.java'
}

spotless {
    java {
        endWithNewline()
        indentWithSpaces()
        removeUnusedImports()
        toggleOffOn()
        eclipse().configFile rootProject.file('codeformat/formatter-config.xml')
        importOrder()

        // courtesy of diffplug/spotless#240
        // https://github.com/diffplug/spotless/issues/240#issuecomment-385206606
        custom 'noWildcardImports', { String fileContents ->
            if (fileContents.contains('*;\n')) {
                throw new GradleException('No wildcard imports are allowed!')
            }
        }
        bumpThisNumberIfACustomStepChanges(1)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'neoforge-junit-utils'
        }
    }
}
