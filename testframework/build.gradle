plugins {
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless'
    id 'net.neoforged.licenser'
    id 'net.neoforged.gradle.platform'
    id 'neoforge.formatting-conventions'
}

java.withSourcesJar()

repositories {
    maven {
        name 'Mojang'
        url 'https://libraries.minecraft.net'
    }
    maven {
        name 'NeoForged'
        url 'https://maven.neoforged.net/releases'
    }
}

repositories {
    maven {
        name 'Maven for PR #150' // https://github.com/neoforged/FancyModLoader/pull/150
        url 'https://prmaven.neoforged.net/FancyModLoader/pr150'
        content {
            includeModule('net.neoforged.fancymodloader', 'junit-fml')
            includeModule('net.neoforged.fancymodloader', 'loader')
            includeModule('net.neoforged.fancymodloader', 'earlydisplay')
        }
    }
}

dependencies {
    implementation project(':neoforge')

    compileOnly(platform("org.junit:junit-bom:${project.jupiter_api_version}"))
    compileOnly "org.junit.jupiter:junit-jupiter-params"

    compileOnly "org.jetbrains:annotations:${project.jetbrains_annotations_version}"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

license {
    header = rootProject.file('codeformat/HEADER.txt')
    include '**/*.java'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources).configureEach {
    inputs.properties version: version

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand version: version
    }
}

publishing {
    publications.create('TestFramework', MavenPublication) {
        groupId = project.group
        artifactId = project.name
        version = project.version

        from components.java
    }
    repositories {
        maven rootProject.gradleutils.getPublishingMaven()
    }
}
