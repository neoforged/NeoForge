import net.neoforged.gradle.dsl.common.runs.task.extensions.TestTaskExtension

plugins {
    id 'net.neoforged.gradle.junit' version '7.0.124-junit' apply false
}

apply plugin: 'net.neoforged.gradle.platform'
apply plugin: 'java'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'net.neoforged.licenser'
apply plugin: 'net.neoforged.gradle.junit'

def neoforgeProject = project(':neoforge')
def testframeworkProject = project(':testframework')

java.toolchain.languageVersion.set(JavaLanguageVersion.of(project.java_version))

repositories {
    mavenLocal()
    maven {
        name 'Mojang'
        url 'https://libraries.minecraft.net'
    }
    maven {
        name 'NeoForged'
        url 'https://maven.neoforged.net/releases'
    }
}

sourceSets {
    main {
        resources {
            srcDir file('src/generated/resources')
        }
    }
}

runTypes {
    junit {
        from neoforgeProject.runTypes.junit
        runAdapter = neoforgeProject.runTypes.junit.runAdapter
    }
}

test {
    useJUnitPlatform()
    extensions.configure(TestTaskExtension) {
        // We need the classes of Neo to be available
        it.testSource neoforgeProject.sourceSets.main
    }
}

dependencies {
    implementation neoforgeProject
    implementation testframeworkProject

    testImplementation(platform("org.junit:junit-bom:${project.jupiter_api_version}"))
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    testImplementation("org.assertj:assertj-core:${project.assertj_core}")
    testImplementation(project(':neoforge-junit-utils'))
    testImplementation "net.neoforged.fancymodloader:junit-fml:${project.fancy_mod_loader_version}"

    compileOnly "org.jetbrains:annotations:${project.jetbrains_annotations_version}"
}

runs {
    client {
        configure neoforgeProject.runTypes.client
    }
    server {
        configure neoforgeProject.runTypes.server
    }
    gameTestServer {
        configure neoforgeProject.runTypes.gameTestServer
    }
    gameTestClient {
        configure neoforgeProject.runTypes.gameTestClient
    }
    data {
        configure neoforgeProject.runTypes.data

        programArguments.addAll '--flat', '--all', '--validate',
                '--mod', 'data_gen_test',
                '--mod', 'global_loot_test',
                '--mod', 'scaffolding_test',
                '--mod', 'custom_tag_types_test',
                '--mod', 'new_model_loader_test',
                '--mod', 'remove_tag_datagen_test',
                '--mod', 'tag_based_tool_types',
                '--mod', 'custom_transformtype_test',
                '--mod', 'data_pack_registries_test',
                '--mod', 'biome_modifiers_test',
                '--mod', 'structure_modifiers_test',
                '--mod', 'custom_preset_editor_test',
                '--mod', 'custom_predicate_test',
                '--mod', 'neotests',
                '--existing-mod', 'testframework',
                '--existing', sourceSets.main.resources.srcDirs[0].absolutePath

        final File gameDir = project.file("runs/${name}") as File
        gameDir.mkdirs();

        workingDirectory.set gameDir
        programArguments.addAll '--gameDir', gameDir.absolutePath
    }
}

//We need the assets and natives tasks from the forge project.
runs.configureEach {
    dependsOn(neoforgeProject.runtime.assets, neoforgeProject.runtime.natives)
    modSource neoforgeProject.sourceSets.main
    modSource testframeworkProject.sourceSets.main
}

afterEvaluate {
    runs.data {
        // Override --output that forge already has
        def args = new ArrayList<String>(programArguments.get());
        def outputIndex = args.indexOf('--output');
        args.set(outputIndex+1, file('src/generated/resources/').absolutePath);
        programArguments.set(args);
    }
}

license {
    header = rootProject.file('codeformat/HEADER.txt')
    skipExistingHeaders = true
    include '**/*.java'
}

spotless {
    java {
        endWithNewline()
        indentWithSpaces()
        removeUnusedImports()
        toggleOffOn()
        eclipse().configFile rootProject.file('codeformat/formatter-config.xml')
        importOrder()

        // courtesy of diffplug/spotless#240
        // https://github.com/diffplug/spotless/issues/240#issuecomment-385206606
        custom 'noWildcardImports', { String fileContents ->
            if (fileContents.contains('*;\n')) {
                throw new GradleException('No wildcard imports are allowed!')
            }
        }
        bumpThisNumberIfACustomStepChanges(1)
    }
}
